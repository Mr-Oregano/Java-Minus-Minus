USE_LR_PARSER

(* Grammar for the Java Minus Minus language 
    Authors:
        Klim Fedorchuk
        Dylan Keefer
        Connor Peterson
        Chaz Brock
*)

(* TODO: As an interesting feature create an 'xor' operator? *)
(* TODO: Add functions and function calls *)
(* TODO: Implement 'input' function *)
(* TODO: Add control flow statements (e.g. 'continue' and 'break') *)

statementList

<statementList> ::= | <statement> <statementList> .

<statement> ::= <openStatement> | <closedStatement> .

<openStatement> ::= "if" "(" <expr> ")" <statement>
                  | "if" "(" <expr> ")" <closedStatement> "else" <openStatement> 
                  | "while" "(" <expr> ")" <openStatement> 
                  | "for" "(" <forInit> ";" <expr> ";" <assignment> ")" <openStatement> .

<closedStatement> ::= "if" "(" <expr> ")" <closedStatement> "else" <closedStatement> 
                    | "while" "(" <expr> ")" <closedStatement>
                    | "for" "(" <forInit> ";" <expr> ";" <assignment> ")" <closedStatement>
                    | <simpleStatement> .

<simpleStatement> ::= <block> 
                    | <assignment> ";"
                    | <init> ";"
                    | <decl> ";" 
                    | "print" "(" <expr> ")" ";"
                    | "println" "(" <expr> ")" ";"
                    | ";" .

(* Basic Statements *)

<assignment> ::= IDENTIFIER "=" <expr> | <decoratedID> .

<init> ::= TYPE IDENTIFIER "=" <expr> .

<decl> ::= TYPE IDENTIFIER .

<block> ::= "{" <statementList> "}" .

(* Basic Loops *)

<forInit> ::= <assignment> | <init> .

(* Basic Expressions *)

<expr> ::= <expr> "or" <andExpr> | <andExpr> .
<andExpr> ::= <andExpr> "and" <equalExpr> | <equalExpr> .

<equalExpr> ::= <equalExpr> "==" <relExpr> 
              | <equalExpr> "!=" <relExpr> 
              | <relExpr> .

<relExpr> ::= <relExpr> "<" <sumExpr> 
            | <relExpr> ">" <sumExpr> 
            | <relExpr> "<=" <sumExpr> 
            | <relExpr> ">=" <sumExpr> 
            | <sumExpr> .

<sumExpr> ::= <sumExpr> "+" <mulExpr> 
            | <sumExpr> "-" <mulExpr> 
            | <mulExpr> .

<mulExpr> ::= <mulExpr> "*" <unaryExpr> 
            | <mulExpr> "/" <unaryExpr>
            | <mulExpr> "%" <unaryExpr>
            | <unaryExpr> .

<unaryExpr> ::= "abs" <unaryExpr> 
              | "not" <unaryExpr> 
              | "~" <unaryExpr> 
              | <expExpr> .

<expExpr> ::= <factor> "^" <expExpr> | <factor> .

<factor> ::= IDENTIFIER 
           | "true"
           | "false" 
           | INT_LITERAL 
           | STR_LITERAL
           | "(" <expr> ")" 
           | <decoratedID> .

<decoratedID> ::= "++" IDENTIFIER 
          | "--" IDENTIFIER
          | IDENTIFIER "++"
          | IDENTIFIER "--" .