USE_LR_PARSER

(* Grammar for the Java Minus Minus language 
    Authors:
        Klim Fedorchuk
        Dylan Keefer
        Connor Peterson
        Chaz Brock
*)

statementList

<statementList> ::= | <statement> <statementList> .

<statement> ::= <block> 
              | <assignment> ";"
              | <decl> ";" 
              | "if" "(" <expr> ")" <block> 
              | "if" "(" <expr> ")" <block> "else" <block>			
              | <whileLoop>
              | <forLoop>
              | "print" "(" <expr> ")" ";"
              | ";" .

(* Basic Statements *)

<assignment> ::= IDENTIFIER "=" <expr> | <decID> .

<decl> ::= "bool" IDENTIFIER | "int" IDENTIFIER .

<block> ::= "{" <statementList> "}" .

(* Basic loops *)
<whileLoop> ::= "while" "(" <expr> ")" <block> .

<forLoop> ::= "for" "(" <assignment> ";" <expr> ";" <assignment> ")" <block> .

(* Basic Expressions *)

<expr> ::= <expr> "or" <andExpr> | <andExpr> .
<andExpr> ::= <andExpr> "and" <equalExpr> | <equalExpr> .

<equalExpr> ::= <equalExpr> "==" <relExpr> 
              | <equalExpr> "!=" <relExpr> 
              | <relExpr> .

<relExpr> ::= <relExpr> "<" <sumExpr> 
            | <relExpr> ">" <sumExpr> 
            | <sumExpr> .

<sumExpr> ::= <sumExpr> "+" <mulExpr> 
            | <sumExpr> "-" <mulExpr> 
            | <mulExpr> .

<mulExpr> ::= <mulExpr> "*" <unaryExpr> 
            | <mulExpr> "/" <unaryExpr>
            | <mulExpr> "%" <unaryExpr>
            | <unaryExpr> .

<unaryExpr> ::= "abs" <unaryExpr> 
              | "not" <unaryExpr> 
              | "~" <unaryExpr> 
              | <expExpr> .

<expExpr> ::= <factor> "^" <expExpr> | <factor> .

<factor> ::= IDENTIFIER 
           | "true"
           | "false" 
           | INT_LITERAL 
           | "(" <expr> ")" 
           | <decID> .

<decID> ::= "++" IDENTIFIER 
          | "--" IDENTIFIER
          | IDENTIFIER "++"
          | IDENTIFIER "--" .