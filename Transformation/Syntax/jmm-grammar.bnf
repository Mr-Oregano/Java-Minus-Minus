USE_LR_PARSER

(* Grammar for the Java Minus Minus language 
	Authors:
		Klim Fedorchuk
		Dylan Keefer
		Connor Peterson
		Chaz Brock
*)

statementList

<statementList> ::= | <statement> <statementList> .

<statement> ::= <expr> ";" 
			  | <block> 
			  | <assignment> ";"
			  | <decl> ";" 
			  | "if" "(" <expr> ")" <block> 
			  | "if" "(" <expr> ")" <block> "else" <block>			
			  | "while" "(" <expr> ")" <block>
			  | "for" "(" <assignment> ";" <expr> ";" <expr> ")" <block> 
			  | "print" "(" <expr> ")" ";"
			  | ";" .

<assignment> ::= IDENTIFIER "=" <expr> .

<decl> ::= "bool" IDENTIFIER | "int" IDENTIFIER .

<block> ::= "{" <statementList> "}" .

(* Basic Expressions *)

<expr> ::= <orExpr> | <incExpr> .

<incExpr> ::= <incOp> <mutable> | <mutable> <incOp> .
<incOp> ::= "++" | "--".

<orExpr> ::= <orExpr> "or" <andExpr> | <andExpr> .
<andExpr> ::= <andExpr> "and" <notExpr> | <notExpr> .
<notExpr> ::= "not" <notExpr> | <relExpr> .					

<relExpr> ::= <relExpr> <relOp> <absExpr> | <absExpr> .
<relOp> ::= "<" | ">" | "==" | "!=" .

<absExpr> ::=  "abs" <absExpr> | <sumExpr> .

<sumExpr> ::= <sumExpr> <addOp> <mulExpr> | <mulExpr> .
<addOp> ::= "+" | "-" .

<mulExpr> ::= <mulExpr> <mulOp> <expExpr> | <expExpr> .
<mulOp> ::= "*" | "/" | "%" .

<expExpr> ::= <unaryExpr> "^" <expExpr> | <unaryExpr> .

<unaryExpr> ::= "~" <factor> | <factor> .

<factor> ::= <mutable> | <immutable> .

<mutable> ::= IDENTIFIER .
<immutable> ::= "true" | "false" | INT_LITERAL | "(" <expr> ")" .